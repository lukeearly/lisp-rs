top_level = _{ SOI ~ sexp ~ EOI }

sexp = _{ plist | number | string | symbol | quote | quasiquote | unquote }
quote = { "'" ~ quotable }
quasiquote = { "`" ~ quotable }
unquote = { "," ~ quotable }
quotable = _{ plist | symbol | quote | quasiquote | unquote }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }

// pair = { pairable ~ ":" ~ pairable }
// pairable = _{ plist | number | string | symbol | quote | quasiquote | unquote }

plist = _{ "(" ~ list ~ ")" }
list = _{ custom_term_list | nil_term_list }
nil_term_list = { list_item* }
custom_term_list = { list_item* ~ "." ~ list_item }
list_item = _{ sexp }

symbol = ${ normal_symbol | special_character }
normal_symbol = _{ (ASCII_ALPHA) ~ (ASCII_ALPHA | ASCII_DIGIT | special_character)* }
special_character = _{ "-" | "+" | "*" | "/" | "\\" | "=" | "^" | "&" | "|" | "~" | "!" }

string = { "\"" ~ inner ~ "\"" }
inner = @{ char* }
char = {
    !("\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

number = _{ octal | hexadecimal | binary | decimal }

decimal = @{
    "-"?
    ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
    ~ ("." ~ ASCII_DIGIT*)?
    ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}

octal = @{
    "-"?
    ~ ("0" ~ ASCII_DIGIT+)
}

hexadecimal = @{
    "-"?
    ~ ("0x" ~ ASCII_DIGIT+)
}

binary = @{
    "-"?
    ~ ("0b" ~ ASCII_DIGIT+)
}	